group 'net.namibsun.themes-moe-dl.lib'
version rootProject.ext.version

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'jacoco'
apply plugin: 'org.jetbrains.dokka'

configurations {
    ktlint
}

dependencies {
    testCompile 'junit:junit:4.12'
    compile "org.jetbrains.kotlin:kotlin-stdlib:1.0.5"
    compile "org.jsoup:jsoup:1.9.2"
    compile "org.jetbrains.dokka:dokka-gradle-plugin:0.9.13"
    ktlint 'com.github.shyiko:ktlint:0.4.0'
}

sourceSets {
    main.java.srcDirs += 'src/main/kotlin/'
    test.java.srcDirs += 'src/test/kotlin/'
}

jar {
    manifest {
        attributes 'Main-Class': 'net.namibsun.themes_moe_dl.lib.MainKt'
    }
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}

dokka {
    moduleName = 'themes-moe-dl-lib'
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc"
    //processConfigurations = ['compile', 'extra']
    includes = ['Module.md']
    //samples = ['samples/basic.kt', 'samples/advanced.kt']
    linkMapping {
        dir = "src/main/kotlin"
        url = "https://gitlab.namibsun.net/namboy94/themes.moe-dl/blob/master/themes-moe-dl-lib/src/main/kotlin"
        suffix = ""
    }
    sourceDirs = files('src/main/kotlin')
}

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = "javadoc"
    outputDirectory = "$buildDir/dokkaJavadoc"
}

jacocoTestReport {
    group = "Reporting"
    reports {
        xml.enabled true
        csv.enabled false
        html.destination "${buildDir}/reports/coverage"
    }
}

task ktlint(type: JavaExec) {
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
}

check.dependsOn ktlint

task ktlintFormat(type: JavaExec) {
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
    ignoreExitValue true
}